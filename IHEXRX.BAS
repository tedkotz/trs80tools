' This will take a intel format hes file sent over the
' serial interface load it into memory and then execute it.
' https://en.wikipedia.org/wiki/Intel_HEX
' SETUP SERIAL
5 DEFINT A-Z
10 IP=-1
20 OF=0                   ' OFFSET=0
30 POKE 16890, 0
40 POKE 16888, 2*17    ' TX/RX BAUDRATE 0=50, 2=110, 5=300, 12=4800, 14=9600, 15=19200
50 U1 = 16526          ' LSB of USR Call Address
60 U2 = 16527          ' MSB of USR Call Address
70 POKE U1,90
80 POKE U2,0
90 X = USR(0)          ' CALL $RSINIT [90]
100 RX = 80
'105 TX = 85
110 CI = 16872           ' INPUT BUFFER
'115 CO = 16880           ' OUTPUT BUFFER

' FLOW CONTROL: ON
' WAIT FOR ":"
120 POKE U1, RX
130 X = USR(0)          ' CALL $RSRCV [80]
140 IF PEEK(CI) <> 58 THEN 130 ' READ INPUT BUFFER

' GET SIZE
150 GOSUB 900
160 SZ=BY

' READ FULL MESSAGE
170 FOR I=1 TO SZ + 4
180 GOSUB 900
190 A(I)=BY
200 NEXT I

' FLOW CONTROL: OFF
' CHECK CHECKSUM
' GOTO ON MESSAGE TYPE
210 ON A(3) GOSUB 400, 1000, 500, 600
220 GOTO 120

' 00:
' SET ADDR=C(3-6)+OFFSET
400 AD = A(1) * 256 + A(2) - 4 + OF
410 IF IP=-1 THEN IP=AD+4
420 FOR I = 4 TO SZ+3
430 POKE AD+I, A(I)
440 NEXT I
450 RETURN

'02
'OF=C(9-12)
500 OF=(A(4) * 256 + A(5)) *16
510 RETURN

'03
'IP=C(14-17)
600 IP=A(6) * 256 + A(7)
610 RETURN

800 X = USR(0)          ' READ CHAR CONVERT AS HEX
810 C = PEEK(CI)
820 IF C==0 THEN 810
830 IF 48<=C AND C<=57 THEN NI=C-48
840 IF 65<=C AND C<=70 THEN NI=C-55
850 IF 97<=C AND C<=102 THEN NI=C-87
860 RETURN

900 POKE U1, RX         ' READ 2 CHARACTERS BUILD A BYTE
910 GOSUB 800
920 BY=NI              ' READ INPUT BUFFER
910 GOSUB 800
940 BY=BY*16+NI  ' READ INPUT BUFFER
950 RETURN

'01
1000 PRINT "DONE. BREAK TO EXIT, ANY KEY TO EXECUTE";IP
1010 IF INKEY$="" THEN 1010
1020 IF IP=-1 THEN END
1030 POKE 16526, IP AND 255
1040 POKE 16527, INT(IP/256)
1050 X=USR(0)                     'CALL TO ADDRESS
1060 END
