5 DEFINT A-Z
10 POKE 16890, 0
15 POKE 16888, 2*17    ' TX/RX BAUDRATE 0=50, 2=110, 5=300, 12=4800, 14=9600, 15=19200
17 U1 = 16526          ' LSB of USR Call Address
18 U2 = 16527          ' MSB of USR Call Address
20 POKE U1,90
30 POKE U2,0
40 X = USR(0)          ' CALL $RSINIT [90]
50 RX = 80
60 TX = 85
70 CI = 16872           ' INPUT BUFFER
80 CO = 16880           ' OUTPUT BUFFER

100 POKE U1, RX
110 X = USR(0)          ' CALL $RSRCV [80]
120 C$ = CHR$(PEEK(CI)) ' READ INPUT BUFFER
130 PRINT C$;           ' WRITE CHAR TO SCREEN will ignore 0

150 C$ = INKEY$         ' READ KEYPRESS
160 IF C$="" THEN 100
170 POKE CO, ASC(C$)    ' WRITE CHAR TO OUTPUT BUFFER
180 POKE U1, TX
190 X = USR(0)          ' CALL $RSTX [85]
200 GOTO 100


